plugins {
	id 'fabric-loom' version '0.2.7-SNAPSHOT'
	id "com.jfrog.bintray" version "1.8.4"
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

if(rootProject.file('private.gradle').exists()) { //Publishing details
	apply from: 'private.gradle'
}

archivesBaseName = project.archives_base_name
version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

bintray {
	user = System.getenv("BINTRAY_USER")
	key = System.getenv("BINTRAY_KEY")
	publications = ['WrenchablePublication']
	pkg {
		repo = 'wrenchable'
		name = archives_base_name
		licenses = ['MIT']
		vcsUrl = 'https://github.com/Zundrel/Wrenchable.git'
		version {
			name = project.mod_version + "+" + project.minecraft_version
			released  = new Date()
			vcsTag = project.mod_version + "+" + project.minecraft_version
		}
	}
}

minecraft {
}

repositories {
	maven { url "https://maven.fabricmc.net/io/github/prospector/modmenu/ModMenu/" }
 	maven {
    	name = "grondag"
    	url = "https://grondag-repo.appspot.com"
    	credentials {
            username "guest"
            password ""
		}
    }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modCompile "net.fabricmc:fabric-loader:${project.loader_version}"

	modCompile "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modImplementation ("grondag:fermion-mc116:${project.fermion_version}") { transitive = false }
	modImplementation ("grondag:fermion-modkeys-mc116:${project.fermion_modkeys_version}") { transitive = false }
	include "grondag:fermion-mc116:${project.fermion_version}"
    include "grondag:fermion-modkeys-mc116:${project.fermion_modkeys_version}"
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

jar {
	from "LICENSE"
}

publishing {
	publications {
		WrenchablePublication(MavenPublication) {
			groupId 'com.zundrel'
			artifactId 'wrenchable'
			version project.mod_version + "+" + project.minecraft_version

			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
			artifact javadocJar
		}
	}

	repositories {
		mavenLocal()
	}
}
